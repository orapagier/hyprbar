pragma ComponentBehavior: Bound
import QtQuick
import "SettingsStyle.js" as Style
import QtQuick.Controls.Basic
import QtQuick.Layouts
import Quickshell
import Quickshell.Io
import "SettingsModel.js" as Model

FloatingWindow {
    id: window
    required property var store
    property var wallpaperSources: ({})
    property string lockingError: ""
    property string powerError: ""
    property var audioServices: null
    property var notificationApps: []
    property var batteryInfo: ({})
    signal lockRequested()
    signal focusRequested()
    // A compositor close hides the native window without clearing the
    // FloatingWindow visibility request. Reset it so the next open remaps.
    onClosed: visible = false
    property var draft: Model.copy(store.defaults)
    property var initial: Model.copy(store.defaults)
    property var submitted: Model.copy(store.defaults)
    property bool editingSession: false
    readonly property bool saving: writer.running
    readonly property bool idle: !writer.running && !autoSave.running && !cleanupPage.busy && !hyprskillPage.busy && !githubSync.running && !keybindingsPage.busy && !themePage.busy && !applicationsPage.busy && !recoveryPage.busy && !powerPage.busy && !soundPage.busy
    onDraftChanged: if (editingSession) autoSave.restart()
    onVisibleChanged: if (!visible && editingSession && dirty) apply()
    property int section: -1
    property var scrollPositions: ({})
    property int previousSection: -1
    property string searchQuery: ""
    property bool barItemsExpanded: false
    readonly property var navigation: [
        {group: "Desktop", pages: [
            {section: -8, title: "Appearance", symbol: "󰔎", keywords: "theme colors font cursor"},
            {section: -2, title: "Windows", symbol: "󰖲", keywords: "hyprland transparency blur gaps animations"},
            {section: -1, title: "Bar & layout", symbol: "󰕮", keywords: "topbar hyprbar spacing workspace workspaces"},
            {section: -13, title: "Applications", symbol: "󰀻", keywords: "default apps startup"},
            {section: -11, title: "Notifications", symbol: "󰂚", keywords: "delivery popups disturb"}
        ]},
        {group: "Devices", pages: [
            {section: -7, title: "Displays", symbol: "󰍹", keywords: "monitor resolution scale rotation"},
            {section: -12, title: "Sound", symbol: "󰕾", keywords: "audio volume microphone speakers"},
            {section: -9, title: "Mouse & keyboard", symbol: "󰍽", keywords: "input touchpad scrolling typing layouts"},
            {section: -6, title: "Keyboard shortcuts", symbol: "󰌌", keywords: "keybindings hotkeys"}
        ]},
        {group: "System", pages: [
            {section: -10, title: "Power & battery", symbol: "󰁹", keywords: "brightness sleep lid profiles"},
            {section: -3, title: "Screen locking", symbol: "󰌾", keywords: "security idle lock"},
            {section: -4, title: "Cleanup", symbol: "󰃢", keywords: "storage files maintenance"},
            {section: -14, title: "Updates & recovery", symbol: "󰁯", keywords: "backup restore checkpoints"},
            {section: -5, title: "Hyprskill", symbol: "󰒓", keywords: "agents coding knowledge"}
        ]}
    ]
    readonly property var barNavigation: draft.items.map((item, index) => ({section: index, title: names[item.id] || item.id, symbol: symbols[item.id] || "󰒓", keywords: "bar item " + item.id, itemEnabled: item.enabled}))
    readonly property var searchResults: navigation.reduce((pages, group) => pages.concat(matchingPages(group.pages)), []).concat(matchingPages(barNavigation))
    function matchingPages(pages) {
        let query = searchQuery.trim().toLowerCase();
        return pages.filter(page => !query || (page.title + " " + page.keywords).toLowerCase().includes(query));
    }
    onSectionChanged: {
        if (editorScroll.contentItem) scrollPositions[previousSection] = editorScroll.contentItem.contentY;
        previousSection = section;
        if (section >= 0) barItemsExpanded = true;
        Qt.callLater(() => {
            if (editorScroll.contentItem) editorScroll.contentItem.contentY = scrollPositions[section] || 0;
        });
        pageFade.restart();
    }
    property string message: ""
    property bool success: true
    property string savedNotice: ""
    property string submittedNotice: ""
    readonly property bool dirty: JSON.stringify(draft) !== JSON.stringify(initial)
    readonly property var selected: section >= 0 ? draft.items[section] : ({})
    readonly property var names: ({
            launcher: "App launcher",
            settings: "Settings shortcut",
            workspaces: "Workspaces",
            media: "Media & spectrum",
            calendar: "Clock & calendar",
            tray: "System tray",
            notifications: "Notifications",
            audio: "Audio",
            wifi: "Wi-Fi",
            bluetooth: "Bluetooth",
            battery: "Battery",
            power: "Power"
        })
    readonly property var symbols: ({
            launcher: "",
            settings: "󰒓",
            workspaces: "󰍹",
            media: "󰎆",
            calendar: "󰃭",
            tray: "󰕰",
            notifications: "󰂚",
            audio: "󰕾",
            wifi: "󰤨",
            bluetooth: "󰂯",
            battery: "󰁹",
            power: "󰐥"
        })
    visible: false
    title: "Hyprshell Settings"
    implicitWidth: screen ? Math.min(1180, Math.round(screen.width * 0.9)) : 1180
    implicitHeight: screen ? Math.min(860, Math.round(screen.height * 0.85)) : 810
    color: Style.background
    function open() {
        minimized = false;
        if (visible) {
            focusRequested();
            return;
        }
        if (editingSession && (dirty || saving)) {
            visible = true;
            return;
        }
        savedNotice = "";
        savedNoticeTimer.stop();
        editingSession = false;
        initial = Model.copy(store.config);
        draft = Model.copy(store.config);
        message = store.error;
        success = !store.error;
        editingSession = true;
        visible = true;
    }
    function updateItem(key, value) {
        let next = Model.copy(draft);
        if (key === "pillGroup") next = Model.setPillGroup(draft, selected.id, value);
        else if (key === "sharedBackground") next = Model.setSharedBackground(draft, selected.id, value);
        else if (key === "side") next = Model.setItemSide(draft, selected.id, value);
        else next.items[section][key] = value;
        draft = next;
    }
    function resetItem() {
        if (saving || section < 0) return;
        draft = Model.restoreItem(draft, initial, selected.id);
    }
    function rearrangeItem(id, side, beforeId) {
        draft = Model.reorder(draft, id, side, beforeId);
    }
    function updateBar(key, value) {
        let next = Model.copy(draft);
        next.bar[key] = value;
        draft = next;
    }
    function updateHypr(key, value) {
        let next = Model.copy(draft);
        if (value === "")
            delete next.hyprland[key];
        else
            next.hyprland[key] = value;
        draft = next;
    }
    function updateNotifications(key, value) {
        let next = Model.copy(draft);
        next.notifications[key] = value;
        draft = next;
    }
    function updatePower(key, value) {
        let next = Model.copy(draft);
        next.power[key] = value;
        draft = next;
    }
    function updateLocking(key, value) {
        let next = Model.copy(draft);
        next.locking[key] = value;
        draft = next;
    }
    function moveItem(direction) {
        let next = Model.copy(draft);
        let current = next.items[section];
        let group = next.items.filter(i => i.side === current.side).sort((a, b) => a.order - b.order);
        let index = group.findIndex(i => i.id === current.id), other = index + direction;
        if (other < 0 || other >= group.length)
            return;
        [group[index], group[other]] = [group[other], group[index]];
        group.forEach((item, n) => item.order = n);
        draft = next;
    }
    function saveNotice(before, after) {
        let labels = [];
        if (JSON.stringify(before.bar) !== JSON.stringify(after.bar)) labels.push("Bar & layout");
        let inputKeys = ["pointerSpeed", "mouseNaturalScroll", "touchpadNaturalScroll", "tapToClick", "disableWhileTyping", "repeatRate", "repeatDelay", "keyboardLayouts", "layoutSwitch"];
        if (inputKeys.some(key => before.hyprland[key] !== after.hyprland[key])) labels.push("Mouse, touchpad & keyboard");
        if (Object.keys(Object.assign({}, before.hyprland, after.hyprland)).some(key => inputKeys.indexOf(key) < 0 && before.hyprland[key] !== after.hyprland[key])) labels.push("Hyprland appearance");
        if (JSON.stringify(before.notifications) !== JSON.stringify(after.notifications)) labels.push("Notification delivery");
        if (JSON.stringify(before.power) !== JSON.stringify(after.power)) labels.push("Power & battery");
        if (JSON.stringify(before.locking) !== JSON.stringify(after.locking)) labels.push("Screen locking");
        after.items.forEach(item => {
            let previous = before.items.find(entry => entry.id === item.id);
            if (JSON.stringify(previous) !== JSON.stringify(item)) labels.push(names[item.id] || item.id);
        });
        return labels.length ? labels.join(", ") + " settings saved!" : "Settings saved!";
    }
    function apply() {
        if (writer.running || !dirty) return;
        if (store.error || !store.loaded) {
            message = store.error;
            success = false;
            return;
        }
        autoSave.stop();
        submitted = Model.copy(draft);
        submittedNotice = saveNotice(initial, submitted);
        savedNotice = "";
        savedNoticeTimer.stop();
        success = true;
        message = "Saving changes…";
        writer.command = ["python3", decodeURIComponent(Qt.resolvedUrl("settings/backend.py").toString().replace(/^file:\/\//, "")), "--save-json", JSON.stringify(submitted), "--expected-json", JSON.stringify(initial)];
        writer.running = true;
    }
    Timer {
        id: autoSave
        interval: 500
        onTriggered: window.apply()
    }
    Timer {
        id: savedNoticeTimer
        interval: 2000
        onTriggered: window.savedNotice = ""
    }
    Process {
        id: writer
        onExited: (exitCode, exitStatus) => {
            if (exitCode !== 0 && window.success) {
                window.success = false;
                window.message = "Could not save settings. Your last saved values are still active.";
            }
            if (window.dirty && (window.success || JSON.stringify(window.draft) !== JSON.stringify(window.submitted))) autoSave.restart();
        }
        stdout: StdioCollector {
            onStreamFinished: {
                try {
                    let result = JSON.parse(text);
                    window.success = result.ok;
                    window.message = result.ok ? "" : result.message;
                    if (result.ok) {
                        window.savedNotice = window.submittedNotice;
                        savedNoticeTimer.restart();
                        window.initial = Model.copy(window.submitted);
                        window.store.config = Model.copy(window.submitted);
                        window.store.reload();
                    }
                } catch (e) {
                    window.success = false;
                    window.message = "Could not save settings. Check that Python 3 is installed.";
                }
            }
        }
        stderr: StdioCollector {
            onStreamFinished: if (text) {
                window.success = false;
                window.message = text;
            }
        }
    }
    property string syncMessage: ""
    property bool syncSuccess: true
    property bool syncReady: false
    property string syncRepository: ""
    property string syncForkUrl: ""
    property string syncCreateUrl: ""
    Process {
        id: githubSync
        property string action: ""
        function request(action, extra) {
            githubSync.action = action;
            window.syncReady = false;
            window.syncForkUrl = "";
            window.syncCreateUrl = "";
            window.syncSuccess = true;
            window.syncMessage = action === "--status" ? "Checking GitHub setup…" : action === "--authenticate" ? "Complete sign-in in the authentication window…" : "Saving live desktop to the local repo, then syncing to GitHub…";
            command = ["python3", decodeURIComponent(Qt.resolvedUrl("settings/github_sync.py").toString().replace(/^file:\/\//, ""))].concat(action ? [action] : []).concat(extra || []);
            running = true;
        }
        stdout: StdioCollector {
            onStreamFinished: {
                try {
                    let result = JSON.parse(text);
                    window.syncSuccess = result.ok;
                    window.syncMessage = result.message;
                    window.syncReady = result.ok && result.ready === true;
                    window.syncRepository = result.repository || "";
                    window.syncForkUrl = result.forkUrl || "";
                    window.syncCreateUrl = result.createUrl || "";
                } catch (e) {
                    window.syncSuccess = false;
                    window.syncMessage = "Could not read GitHub sync results.";
                }
            }
        }
        stderr: StdioCollector { onStreamFinished: if (text) { window.syncSuccess = false; window.syncMessage = text; } }
        onExited: (code, status) => {
            if (code !== 0 && window.syncSuccess) {
                window.syncSuccess = false;
                window.syncMessage = "GitHub sync failed. Check Git and GitHub authentication.";
            }
        }
    }
    Shortcut {
        sequence: "Ctrl+F"
        enabled: window.visible
        onActivated: { searchField.forceActiveFocus(); searchField.selectAll(); }
    }
    NumberAnimation {
        id: pageFade
        target: pageContent
        property: "opacity"
        from: 0.65
        to: 1
        duration: window.draft.hyprland.animations === false ? 0 : 140
        easing.type: Easing.OutCubic
    }
    Popup {
        id: githubDialog
        objectName: "githubSyncDialog"
        anchors.centerIn: parent
        width: Math.min(480, window.width - 40)
        padding: 24
        modal: true
        closePolicy: githubSync.running ? Popup.NoAutoClose : Popup.CloseOnEscape | Popup.CloseOnPressOutside
        background: Rectangle { color: Style.surface; radius: 14; border.color: Style.controlBorder }
        contentItem: ColumnLayout {
            spacing: 14
            Label { text: "Sync to GitHub"; color: Style.text; font.pixelSize: 20; font.bold: true }
            Label { objectName: "githubRepositoryLabel"; text: window.syncRepository || "Sign in to find your repository"; color: Style.accentText }
            Label { Layout.fillWidth: true; text: window.syncMessage; wrapMode: Text.WordWrap; color: window.syncSuccess ? Style.muted : Style.danger }
            RowLayout {
                SettingsButton {
                    objectName: "githubForkButton"
                    text: "Fork Hyprshell"
                    visible: window.syncForkUrl !== ""
                    enabled: !githubSync.running
                    onClicked: Qt.openUrlExternally(window.syncForkUrl)
                }
                SettingsButton {
                    objectName: "githubCreateButton"
                    text: "Create repository"
                    visible: window.syncCreateUrl !== ""
                    enabled: !githubSync.running
                    onClicked: Qt.openUrlExternally(window.syncCreateUrl)
                }
            }
            RowLayout {
                Layout.fillWidth: true
                spacing: 8
                SettingsButton {
                    objectName: "githubCheckButton"
                    Layout.fillWidth: true
                    Layout.preferredWidth: 1
                    implicitWidth: 0
                    leftPadding: 8; rightPadding: 8
                    text: "Check again"
                    enabled: !githubSync.running
                    onClicked: githubSync.request("--status", [])
                }
                SettingsButton {
                    Layout.fillWidth: true
                    Layout.preferredWidth: 1
                    implicitWidth: 0
                    leftPadding: 8; rightPadding: 8
                    text: "Authenticate"
                    enabled: !githubSync.running
                    onClicked: githubSync.request("--authenticate", [])
                }
                SettingsButton {
                    objectName: "githubConfirmSync"
                    Layout.fillWidth: true
                    Layout.preferredWidth: 1
                    implicitWidth: 0
                    leftPadding: 8; rightPadding: 8
                    text: githubSync.running ? "Working…" : "Sync"
                    highlighted: true
                    enabled: !githubSync.running && window.syncReady && !window.dirty && !window.saving && window.success
                    onClicked: githubSync.request("", [])
                }
                SettingsButton {
                    Layout.fillWidth: true
                    Layout.preferredWidth: 1
                    implicitWidth: 0
                    leftPadding: 8; rightPadding: 8
                    text: "Close"
                    enabled: !githubSync.running
                    onClicked: githubDialog.close()
                }
            }
        }
    }
    Pane {
        objectName: "settingsRoot"
        anchors.fill: parent
        padding: 0
        property bool settingsMotionEnabled: window.draft.hyprland.animations !== false
        palette.window: Style.background
        palette.windowText: Style.text
        palette.text: Style.text
        palette.base: Style.surface
        palette.button: Style.button
        palette.buttonText: Style.text
        palette.highlight: Style.accent
        palette.highlightedText: Style.onAccent
        palette.brightText: Style.onAccent
        palette.dark: Style.accentText
        palette.mid: Style.border
        font.family: "Noto Sans"
        font.pixelSize: Style.bodySize
        background: Rectangle { color: Style.background }
        contentItem: ColumnLayout {
            spacing: 0
            RowLayout {
                Layout.fillWidth: true
                Layout.fillHeight: false
                Layout.preferredHeight: 64
                Layout.leftMargin: 22
                Layout.rightMargin: 22
                ColumnLayout {
                    spacing: 2
                    Label {
                        text: "Settings"
                        font.pixelSize: 18
                        font.bold: true
                        color: Style.text
                    }
                    Label {
                        text: "Hyprshell"
                        font.pixelSize: Style.captionSize
                        color: Style.muted
                    }
                }
                Item {
                    Layout.fillWidth: true
                }
                SettingsButton {
                    objectName: "githubSyncButton"
                    text: githubSync.running ? "Syncing…" : "Sync to GitHub"
                    enabled: !githubSync.running && !window.dirty && !window.saving && window.success && window.store.loaded && !cleanupPage.busy && !recoveryPage.busy
                    ToolTip.visible: hovered
                    ToolTip.text: "Commit and push your desktop setup to your GitHub hyprshell repository"
                    onClicked: {
                        githubDialog.open();
                        githubSync.request("--status", []);
                    }
                }
                SettingsButton {
                    text: "Reset unsaved changes"
                    visible: !window.success && window.dirty
                    enabled: !writer.running
                    onClicked: {
                        autoSave.stop();
                        window.draft = Model.copy(window.initial);
                        window.success = true;
                        window.message = "Restored your last saved settings.";
                    }
                }
            }
            Rectangle { Layout.fillWidth: true; implicitHeight: 1; color: Style.separator }
            RowLayout {
                enabled: !githubSync.running && !recoveryPage.busy
                Layout.fillWidth: true
                Layout.fillHeight: true
                spacing: 0
                ScrollView {
                    id: sidebar
                    objectName: "settingsSidebar"
                    Layout.preferredWidth: window.width < 900 ? 228 : 250
                    Layout.fillHeight: true
                    clip: true
                    padding: 12
                    contentWidth: availableWidth
                    ScrollBar.horizontal.policy: ScrollBar.AlwaysOff
                    background: Rectangle { color: Style.sidebar }
                    ColumnLayout {
                        width: sidebar.availableWidth
                        spacing: 4
                        TextField {
                            id: searchField
                            objectName: "settingsSearch"
                            Layout.fillWidth: true
                            Layout.bottomMargin: 10
                            implicitHeight: 38
                            placeholderText: "Search settings"
                            color: Style.text
                            placeholderTextColor: Style.muted
                            font.pixelSize: Style.bodySize
                            leftPadding: 12
                            rightPadding: searchClear.visible ? 38 : 12
                            selectByMouse: true
                            Accessible.name: "Search settings"
                            onTextChanged: window.searchQuery = text
                            onAccepted: if (window.searchResults.length) window.section = window.searchResults[0].section
                            Keys.onEscapePressed: event => {
                                if (text.length) { clear(); event.accepted = true; }
                                else event.accepted = false;
                            }
                            background: Rectangle {
                                radius: Style.controlRadius
                                color: Style.field
                                border.width: searchField.activeFocus ? 2 : 1
                                border.color: searchField.activeFocus ? Style.accentText : Style.border
                            }
                            ToolButton {
                                id: searchClear
                                objectName: "settingsSearchClear"
                                anchors.right: parent.right
                                anchors.verticalCenter: parent.verticalCenter
                                width: 34; height: 34
                                text: "×"
                                visible: searchField.text.length > 0
                                Accessible.name: "Clear search"
                                onClicked: { searchField.clear(); searchField.forceActiveFocus(); }
                                contentItem: Text { text: searchClear.text; color: Style.muted; font.pixelSize: 20; horizontalAlignment: Text.AlignHCenter; verticalAlignment: Text.AlignVCenter }
                                background: Rectangle { radius: 6; color: searchClear.hovered ? Style.hover : "transparent"; border.color: searchClear.activeFocus ? Style.accentText : "transparent" }
                            }
                        }
                        Repeater {
                            model: window.navigation
                            delegate: ColumnLayout {
                                id: navGroup
                                required property var modelData
                                readonly property var pages: window.matchingPages(modelData.pages)
                                visible: pages.length > 0
                                Layout.fillWidth: true
                                spacing: 3
                                Label {
                                    text: navGroup.modelData.group
                                    Layout.leftMargin: 12
                                    Layout.topMargin: 12
                                    Layout.bottomMargin: 5
                                    color: Style.muted
                                    font.pixelSize: Style.captionSize
                                    font.weight: Font.DemiBold
                                }
                                Repeater {
                                    model: navGroup.pages
                                    delegate: SettingsNavButton {
                                        required property var modelData
                                        objectName: "settingsNav" + modelData.section
                                        text: modelData.title
                                        symbol: modelData.symbol
                                        Layout.fillWidth: true
                                        highlighted: window.section === modelData.section
                                        onClicked: window.section = modelData.section
                                    }
                                }
                            }
                        }
                        SettingsButton {
                            objectName: "settingsBarItemsToggle"
                            Layout.fillWidth: true
                            Layout.topMargin: 16
                            flat: true
                            text: "Bar items  " + (window.barItemsExpanded || window.searchQuery.trim() ? "⌃" : "⌄")
                            visible: window.matchingPages(window.barNavigation).length > 0
                            Accessible.description: window.barItemsExpanded ? "Expanded" : "Collapsed"
                            onClicked: window.barItemsExpanded = !window.barItemsExpanded
                        }
                        Repeater {
                            model: window.barItemsExpanded || window.searchQuery.trim() ? window.matchingPages(window.barNavigation) : []
                            delegate: SettingsNavButton {
                                required property var modelData
                                text: modelData.title
                                symbol: modelData.symbol
                                itemEnabled: modelData.itemEnabled
                                Layout.fillWidth: true
                                highlighted: window.section === modelData.section
                                onClicked: window.section = modelData.section
                            }
                        }
                        Label {
                            objectName: "settingsSearchEmpty"
                            visible: window.searchResults.length === 0
                            Layout.fillWidth: true
                            Layout.margins: 12
                            text: "No settings found. Try another search."
                            color: Style.muted
                            font.pixelSize: Style.bodySize
                            wrapMode: Text.WordWrap
                        }
                    }
                }
                Rectangle { Layout.fillHeight: true; implicitWidth: 1; color: Style.separator }
                ScrollView {
                    id: editorScroll
                    objectName: "settingsEditorScroll"
                    Layout.fillWidth: true
                    Layout.fillHeight: true
                    clip: true
                    contentWidth: availableWidth
                    padding: window.width < 900 ? 22 : 36
                    ScrollBar.horizontal.policy: ScrollBar.AlwaysOff
                    background: Rectangle { color: Style.background }
                    ColumnLayout {
                        id: pageContent
                        objectName: "settingsPageContent"
                        width: Math.min(840, editorScroll.availableWidth)
                        x: Math.max(0, (editorScroll.availableWidth - width) / 2)
                        spacing: 24
                        Label {
                            objectName: "settingsPageTitle"
                            text: window.section >= 0 ? window.names[window.selected.id] || "" : window.navigation.reduce((pages, group) => pages.concat(group.pages), []).find(page => page.section === window.section)?.title || "Settings"
                            font.pixelSize: Style.titleSize
                            font.bold: true
                            Layout.fillWidth: true
                            wrapMode: Text.WordWrap
                        }
                        Label {
                            Layout.fillWidth: true
                            Layout.topMargin: -14
                            font.pixelSize: Style.bodySize
                            text: window.section === -14 ? "Keep your system updated, recover saved desktop settings, and track your personal backup checks." : window.section === -13 ? "Choose which apps open files and which start when you log in." : window.section === -12 ? "Choose sound devices, check your microphone, and adjust application volumes." : window.section === -11 ? "Choose popups, Do Not Disturb, and preferences for each application." : window.section === -10 ? "Adjust brightness, inactivity timers, laptop lid behavior, and supported power profiles." : window.section === -9 ? "Adjust pointing, scrolling, typing, and keyboard layouts. Changes save and apply automatically." : window.section === -8 ? "Choose application colors, fonts, and cursors." : window.section === -7 ? "Set resolution, refresh rate, scale, rotation, and monitor positions. Confirm changes before they are saved." : window.section === -6 ? "Record a shortcut, choose its action, and save. Changes appear in Super + K." : window.section === -5 ? "Give your coding agents reusable knowledge of your machine." : window.section === -4 ? "Schedule cleanup and review files before removing them." : window.section === -3 ? "Choose your lock screen and when it activates." : window.section === -1 ? "Set the layout and default appearance for your desktop bar." : window.section === -2 ? "Fine-tune transparency, frosted glass, and window details. Changes save and apply automatically." : "Customize this item. Empty fields follow the original appearance."
                            wrapMode: Text.WordWrap
                            color: Style.muted
                        }
                        Rectangle {
                            objectName: "settingsBarPreview"
                            Layout.fillWidth: true
                            visible: window.section >= -1
                            Layout.preferredHeight: previewBar.y + previewBar.height * previewBar.scale + 16
                            radius: Style.radius
                            color: Style.surface
                            border.color: Style.border
                            Label {
                                id: previewLabel
                                x: 16
                                y: 12
                                width: parent.width - 32
                                wrapMode: Text.WordWrap
                                text: "Bar preview · drag to rearrange, click to customize"
                                color: Style.muted
                                font.pixelSize: Style.captionSize
                                font.letterSpacing: 0
                            }
                            Bar {
                                id: previewBar
                                x: 12
                                y: previewLabel.y + previewLabel.height + 12
                                width: Math.max(1000, parent.width - 24)
                                scale: Math.min(1, (parent.width - 24) / width)
                                transformOrigin: Item.TopLeft
                                settings: window.draft
                                wallpaperSource: window.screen ? window.wallpaperSources[window.screen.name] || "" : ""
                                trayModel: [{icon: Qt.resolvedUrl("icons/preview-tray.svg")} ]
                                clockText: Qt.formatDateTime(new Date(), window.draft.bar.clockFormat)
                                statusData: ({
                                        workspaces: [
                                            {
                                                id: 1,
                                                name: "1",
                                                active: true
                                            },
                                            {
                                                id: 2,
                                                name: "2"
                                            }
                                        ],
                                        audio: {
                                            text: "64%",
                                            icon: "󰕾"
                                        },
                                        network: {
                                            text: "󰤨",
                                            connected: true
                                        },
                                        bluetooth: {
                                            text: "󰂯",
                                            powered: true
                                        },
                                        battery: {
                                            present: true,
                                            text: "󰁹 86%"
                                        }
                                    })
                                mediaData: ({
                                        playing: true,
                                        title: "Your favorite track",
                                        levels: [0.2, 0.5, 0.8, 0.4, 0.9, 0.6, 0.3, 0.7, 0.5, 0.9, 0.3, 0.6]
                                    })
                                notificationData: ({
                                        count: 2
                                    })
                                PreviewLayoutEditor {
                                    anchors.fill: parent
                                    bar: previewBar
                                    names: window.names
                                    onItemSelected: id => window.section = window.draft.items.findIndex(i => i.id === id)
                                    onItemDropped: (id, side, beforeId) => window.rearrangeItem(id, side, beforeId)
                                }
                            }
                        }
                        SettingsBarEditor {
                            visible: window.section === -1
                            Layout.fillWidth: true
                            settings: window.draft.bar
                            onEdited: (key, value) => window.updateBar(key, value)
                        }
                        SettingsItemEditor {
                            visible: window.section >= 0
                            Layout.fillWidth: true
                            settings: window.selected
                            allItems: window.draft.items
                            barSettings: window.draft.bar
                            saving: window.saving
                            onEdited: (key, value) => window.updateItem(key, value)
                            onMoveRequested: direction => window.moveItem(direction)
                            onResetRequested: window.resetItem()
                        }
                        SettingsLocking {
                            visible: window.section === -3
                            Layout.fillWidth: true
                            settings: window.draft.locking
                            saved: !window.dirty && !window.saving && window.success
                            runtimeError: window.lockingError
                            onLockRequested: window.lockRequested()
                            onEdited: (key, value) => window.updateLocking(key, value)
                        }
                        SettingsTheme {
                            id: themePage
                            visible: window.section === -8
                            Layout.fillWidth: true
                        }
                        SettingsApplications {
                            id: applicationsPage
                            visible: window.section === -13
                            Layout.fillWidth: true
                        }
                        SettingsRecovery {
                            id: recoveryPage
                            visible: window.visible && window.section === -14
                            Layout.fillWidth: true
                            ready: !window.dirty && !window.saving && window.success && window.store.loaded
                            onRestored: settings => {
                                window.editingSession = false;
                                window.initial = Model.copy(settings);
                                window.draft = Model.copy(settings);
                                window.store.config = Model.copy(settings);
                                window.store.reload();
                                window.editingSession = true;
                            }
                        }
                        SettingsDisplays {
                            visible: window.section === -7
                            Layout.fillWidth: true
                        }
                        SettingsKeybindings {
                            id: keybindingsPage
                            targetWindow: window
                            visible: window.section === -6
                            Layout.fillWidth: true
                            onEditorRequested: if (editorScroll.contentItem) editorScroll.contentItem.contentY = 0
                        }
                        SettingsCleanup {
                            id: cleanupPage
                            visible: window.section === -4
                            Layout.fillWidth: true
                        }
                        SettingsHyprskill {
                            id: hyprskillPage
                            visible: window.section === -5
                            Layout.fillWidth: true
                        }
                        SettingsPower {
                            id: powerPage
                            visible: window.section === -10
                            Layout.fillWidth: true
                            settings: window.draft.power
                            runtimeError: window.powerError
                            batteryInfo: window.batteryInfo
                            onEdited: (key, value) => window.updatePower(key, value)
                        }
                        SettingsNotifications {
                            visible: window.section === -11
                            Layout.fillWidth: true
                            settings: window.draft.notifications
                            applications: window.notificationApps
                            onEdited: (key, value) => window.updateNotifications(key, value)
                        }
                        SoundTest {
                            id: soundTester
                            sink: window.audioServices ? window.audioServices.sink : null
                            visible: window.visible && window.section === -12
                        }
                        SettingsSound {
                            id: soundPage
                            visible: window.visible && window.section === -12
                            Layout.fillWidth: true
                            services: window.audioServices
                            testSound: soundTester
                        }
                        SettingsInput {
                            visible: window.section === -9
                            Layout.fillWidth: true
                            settings: window.draft.hyprland
                            onEdited: (key, value) => window.updateHypr(key, value)
                        }
                        SettingsAppearance {
                            visible: window.section === -2
                            Layout.fillWidth: true
                            settings: window.draft.hyprland
                            onEdited: (key, value) => window.updateHypr(key, value)
                        }
                    }
                }
            }
            Label {
                Layout.fillWidth: true
                objectName: "settingsSaveNotice"
                Layout.minimumHeight: 30
                Layout.leftMargin: 16
                Layout.rightMargin: 16
                topPadding: 7
                bottomPadding: 7
                text: !window.success ? (window.message || window.store.error) : window.saving ? "Saving changes…" : window.savedNotice || "Changes save automatically"
                horizontalAlignment: Text.AlignHCenter
                wrapMode: Text.WordWrap
                font.pixelSize: Style.captionSize
                color: window.success ? Style.muted : Style.danger
            }
        }
    }
}
